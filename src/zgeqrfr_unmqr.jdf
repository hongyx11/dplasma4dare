extern "C" %{
/*
 * Copyright (c) 2010-2020 The University of Tennessee and The University
 *                         of Tennessee Research Foundation. All rights
 *                         reserved.
 * Copyright (c) 2013      Inria. All rights reserved.
 * $COPYRIGHT
 *
 *
 * @precisions normal z -> s d c
 *
 */
#include "dplasmajdf.h"
#include "dplasma/types.h"
#include "parsec/data_dist/matrix/matrix.h"

%}

descA  [type = "parsec_tiled_matrix_t*"]
descC  [type = "parsec_tiled_matrix_t*" aligned=descA]
descT  [type = "parsec_tiled_matrix_t*" aligned=descA]
p_work [type = "parsec_memory_pool_t *" size = "(sizeof(dplasma_complex64_t)*descT->mb*descT->nb)"]

minMNT [type = "int" hidden=on default = "dplasma_imin(descA->nt, ((descA->m+descA->nb-1)/descA->nb))-1"]
ib     [type = "int" hidden=on default = "descT->mb" ]


zunmqr_zunmqr(k, n)
  /* Execution space */
  k = 0 .. minMNT
  n = 0 .. descC->nt-1

  : descC(0, n)

  READ  V <- A read_A(k)
  READ  T <- T read_T(k)                                 [type_remote = LITTLE_T]
  RW    C <- (k == 0) ? descC(0, n) : C zunmqr_zunmqr(k-1, n)
          -> (k == descA->nt-1) ? descC(0, n)
          -> (k <  descA->nt-1) ? C zunmqr_zunmqr(k+1, n)

BODY
{
    dplasma_complex64_t *lV = (dplasma_complex64_t*)V;
    dplasma_complex64_t *lC = (dplasma_complex64_t*)C;
    int tempm = descC->m - k * descC->nb;
    int tempnn = (n == (descC->nt-1)) ? (descC->n - n * descC->nb) : descC->nb;
    int tempkk = (k == (descA->nt-1)) ? (descA->n - k * descA->nb) : descA->nb;
    int ldv = BLKLDD( descA, 0 );
    int ldc = BLKLDD( descC, 0 );

    printlog("CORE_zunmqr(%d, %d)\n", k, n);

    void *p_elem_W = parsec_private_memory_pop( p_work );

    CORE_zunmqr( dplasmaLeft, dplasmaConjTrans,
                 tempm, tempnn, tempkk, ib,
                 lV + k * descA->nb, ldv,
                 T,                 descT->mb,
                 lC + k * descA->nb, ldc,
                 p_elem_W, descA->nb);

    parsec_private_memory_push( p_work, p_elem_W );
}
END

read_A(k) [profile = off]
  k = 0 .. minMNT

  : descA(0, k)

  RW A <- descA(0, k)
       -> V zunmqr_zunmqr(k, 0 .. descC->nt-1)
BODY
{
    /* nothing */
}
END

read_T(k) [profile = off]
  k = 0 .. descT->nt-1

  : descT(0, k)

  RW T <- descT(0, k)                              /*[type = LITTLE_T type_data = LITTLE_T]*/
       -> T zunmqr_zunmqr(k, 0 .. descC->nt-1)       [type_remote = LITTLE_T]
BODY
{
    /* nothing */
}
END

extern "C" %{

/**
 *******************************************************************************
 *
 * @ingroup dplasma_complex64
 *
 *  dplasma_zunmqr_New - Generates the parsec taskpool that overwrites the general
 *  M-by-N matrix C with
 *
 *                  SIDE = 'L'     SIDE = 'R'
 *  TRANS = 'N':      Q * C          C * Q
 *  TRANS = 'C':      Q**H * C       C * Q**H
 *
 *  where Q is a unitary matrix defined as the product of k elementary
 *  reflectors
 *
 *        Q = H(1) H(2) . . . H(k)
 *
 *  as returned by dplasma_zgeqrf(). Q is of order M if side = dplasmaLeft
 *  and of order N if side = dplasmaRight.
 *
 * WARNING: The computations are not done by this call.
 *
 *******************************************************************************
 *
 * @param[in] side
 *          @arg dplasmaLeft:  apply Q or Q**H from the left;
 *          @arg dplasmaRight: apply Q or Q**H from the right.
 *
 * @param[in] trans
 *          @arg dplasmaNoTrans:   no transpose, apply Q;
 *          @arg dplasmaConjTrans: conjugate transpose, apply Q**H.
 *
 * @param[in] A
 *          Descriptor of the matrix A of size M-by-K if side == dplasmaLeft, or
 *          N-by-K if side == dplasmaRight factorized with the
 *          dplasma_zgeqrf_New() routine.
 *          On entry, the i-th column must contain the vector which
 *          defines the elementary reflector H(i), for i = 1,2,...,k, as
 *          returned by dplasma_zgeqrf_New() in the first k columns of its array
 *          argument A.
 *          If side == dplasmaLeft,  M >= K >= 0.
 *          If side == dplasmaRight, N >= K >= 0.
 *
 * @param[in] T
 *          Descriptor of the matrix T distributed exactly as the A matrix. T.mb
 *          defines the IB parameter of tile QR algorithm. This matrix must be
 *          of size A.mt * T.mb - by - A.nt * T.nb, with T.nb == A.nb.
 *          This matrix is initialized during the call to dplasma_zgeqrf_New().
 *
 * @param[in,out] C
 *          Descriptor of the M-by-N matrix C.
 *          On exit, the matrix C is overwritten by the result.
 *
 *******************************************************************************
 *
 * @return
 *          \retval The parsec taskpool which describes the operation to perform
 *                  NULL if one of the parameter is incorrect
 *
 *******************************************************************************
 *
 * @sa dplasma_zunmqr_Destruct
 * @sa dplasma_zunmqr
 * @sa dplasma_cunmqr_New
 * @sa dplasma_dormqr_New
 * @sa dplasma_sormqr_New
 * @sa dplasma_zgeqrf_New
 *
 ******************************************************************************/
parsec_taskpool_t*
dplasma_zgeqrfr_unmqr_New( parsec_tiled_matrix_t *A,
                           parsec_tiled_matrix_t *T,
                           parsec_tiled_matrix_t *C,
                           void *p_work )
{
    parsec_taskpool_t* tp;

    tp = (parsec_taskpool_t*)parsec_zgeqrfr_unmqr_new( A,
                                                       C,
                                                       T,
                                                       (parsec_memory_pool_t*)p_work );

    /* Default type */
    dplasma_add2arena_rectangle( &((parsec_zgeqrfr_unmqr_taskpool_t*)tp)->arenas_datatypes[PARSEC_zgeqrfr_unmqr_DEFAULT_ADT_IDX],
                                 A->mb*A->nb*sizeof(dplasma_complex64_t),
                                 PARSEC_ARENA_ALIGNMENT_SSE,
                                 parsec_datatype_double_complex_t, A->mb , A->nb, -1);

    /* Little T */
    dplasma_add2arena_rectangle( &((parsec_zgeqrfr_unmqr_taskpool_t*)tp)->arenas_datatypes[PARSEC_zgeqrfr_unmqr_LITTLE_T_ADT_IDX],
                                 T->mb*T->nb*sizeof(dplasma_complex64_t),
                                 PARSEC_ARENA_ALIGNMENT_SSE,
                                 parsec_datatype_double_complex_t, T->mb, T->nb, -1);

    return tp;
}

/**
 *******************************************************************************
 *
 * @ingroup dplasma_complex64
 *
 *  dplasma_zunmqr_Destruct - Free the data structure associated to an taskpool
 *  created with dplasma_zunmqr_New().
 *
 *******************************************************************************
 *
 * @param[in,out] taskpool
 *          On entry, the taskpool to destroy.
 *          On exit, the taskpool cannot be used anymore.
 *
 *******************************************************************************
 *
 * @sa dplasma_zunmqr_New
 * @sa dplasma_zunmqr
 *
 ******************************************************************************/
void
dplasma_zgeqrfr_unmqr_Destruct( parsec_taskpool_t *tp )
{
    parsec_zgeqrfr_unmqr_taskpool_t *parsec_zunmqr_panel = (parsec_zgeqrfr_unmqr_taskpool_t *)tp;

    dplasma_matrix_del2arena( &parsec_zunmqr_panel->arenas_datatypes[PARSEC_zgeqrfr_unmqr_DEFAULT_ADT_IDX ] );
    dplasma_matrix_del2arena( &parsec_zunmqr_panel->arenas_datatypes[PARSEC_zgeqrfr_unmqr_LITTLE_T_ADT_IDX] );

    parsec_taskpool_free(tp);
}

%}
